plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "8.3.0"
}

group = 'de.kevin_stefan'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT")
    compileOnly("org.hibernate.orm:hibernate-core:7.0.0.Final")
    compileOnly("org.hibernate.orm:hibernate-c3p0:7.0.0.Final")
    compileOnly("org.hibernate.orm:hibernate-community-dialects:7.0.0.Final")
    compileOnly("dev.dejvokep:boosted-yaml:1.3.7")
    compileOnly("dev.dejvokep:boosted-yaml-spigot:1.4")
    implementation("dev.jorel:commandapi-bukkit-shade:10.1.2")
    compileOnly("dev.jorel:commandapi-annotations:10.1.2")
    annotationProcessor("dev.jorel:commandapi-annotations:10.1.2")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.5")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand project.properties
    }
}

shadowJar {
    archiveClassifier = ""
    relocate 'dev.jorel.commandapi', 'de.kevin_stefan.virtualChests.commandapi'
}

tasks.build.dependsOn(shadowJar)
